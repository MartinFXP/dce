from snakemake.remote.HTTP import RemoteProvider as HTTPRemoteProvider


# setup
HTTP = HTTPRemoteProvider()

configfile: 'config.yaml'
workdir: config['working_directory']


# rule definitions
rule all:
    input:
        'summary/enrichments.csv',
        expand(
            'plots/{project}/{project}.{pathway}.dce.pdf',
            project=config['projects'], pathway=config['pathways']),
        expand('drugs/results_{pathway}.csv', pathway=config['pathways'])


rule download_TCGA_data:
    output:
        data_dir = directory('tcga_data/GDCdata/'),
        hdf5_dname = directory('tcga_data/hdf5_store/')
    script:
        'scripts/tcga_download.R'


rule process_TCGA_data:
    input:
        hdf5_dname = 'tcga_data/hdf5_store/'
    output:
        expression_fname = 'tcga_data/{project}/expression_matrix.csv',
        classification_fname = 'tcga_data/{project}/case_classifications.csv'
    log:
        notebook = 'notebooks/ProcessTCGAData.{project}.ipynb'
    notebook:
        'notebooks/ProcessTCGAData.ipynb'


rule download_KEGG_pathway:
    output:
        xml_fname = 'kegg_data/{pathway}.xml',
        network_fname = 'kegg_data/{pathway}.edgelist.csv.gz',
        plot_fname = 'kegg_data/{pathway}.pdf',
        geneid_fname = 'kegg_data/{pathway}_geneids.csv'
    script:
        'scripts/kegg_download.R'


rule compute_differential_causal_effects:
    input:
        expression_fname = 'tcga_data/{project}/expression_matrix.csv',
        classification_fname = 'tcga_data/{project}/case_classifications.csv',
        network_fname = 'kegg_data/{pathway}.edgelist.csv.gz'
    output:
        dce_fname = 'results/{project}/{project}.{pathway}.dce.rds',
    script:
        'scripts/compute_dce.R'


rule visualize:
    input:
        dce_fname = 'results/{project}/{project}.{pathway}.dce.rds',
        geneid_fname = 'kegg_data/{pathway}_geneids.csv'
    output:
        plot_fname = 'plots/{project}/{project}.{pathway}.dce.pdf',
        heatmap_fname = 'plots/{project}/{project}.{pathway}.dce.heatmap.pdf'
    script:
        'scripts/visualize.R'


rule discover_drugs:
    input:
        hetnet_nodes_fname = HTTP.remote(
            'https://github.com/hetio/hetionet/raw/master/hetnet/tsv/hetionet-v1.0-nodes.tsv',
            keep_local=True
        )[0],
        hetnet_edges_fname = HTTP.remote(
            'https://github.com/hetio/hetionet/raw/master/hetnet/tsv/hetionet-v1.0-edges.sif.gz',
            keep_local=True
        )[0]
    output:
        result_fname = 'drugs/results_{pathway}.csv'
    script:
        'scripts/drug_discovery.py'


rule summarize:
    input:
        dce_fname_list = expand(
            'results/{project}/{project}.{pathway}.dce.rds',
            project=config['projects'], pathway=config['pathways'])
    output:
        enrichment_fname = 'summary/enrichments.csv'
    script:
        'scripts/summarize_results.R'
