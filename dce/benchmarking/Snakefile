configfile: 'config.yaml'
localrules: all, aggregate_replicates


rule all:
    input:
        expand('benchmarks/plots_{varied_param}/', varied_param=config['benchmarks'].keys())


rule run_benchmark:
    output:
        fname = 'benchmarks/replicates/bench_{varied_param}_{replicate}.csv'
    params:
        benchmark_script = srcdir('main.R')
    resources:
        mem_mb = 5_000
    run:
        import sh

        sh.Rscript(
            params.benchmark_script,
            '--variable', wildcards.varied_param,
            '--values', config['benchmarks'][wildcards.varied_param],
            '--replicates', config['script_replicates'],
            '--output', output.fname,
            _fg=True
        )


rule aggregate_replicates:
    input:
        fname_list = expand(
            'benchmarks/replicates/bench_{varied_param}_{replicate}.csv',
            replicate=list(range(config['rule_replicates'])),
            allow_missing=True)
    output:
        fname = 'benchmarks/bench_{varied_param}.csv'
    run:
        import pandas as pd

        df_list = [pd.read_csv(fname) for fname in input.fname_list]
        pd.concat(df_list).to_csv(output.fname, index=False)


rule plot_results:
    input:
        fname = 'benchmarks/bench_{varied_param}.csv'
    output:
        outdir = directory('benchmarks/plots_{varied_param}/')
    params:
        plotting_script = srcdir('plotting.R')
    run:
        import sh

        sh.Rscript(
            params.plotting_script,
            '--input', input.fname,
            '--output', output.outdir,
            _fg=True
        )
