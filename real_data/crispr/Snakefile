import os


configfile: 'config.yaml'
workdir: config['workdir']

base_dir = srcdir('.')  # `srcdir` in params returns `None`
all_perturbed_genes = list({e.name.split('.')[0].split('_')[1]
                            for e in os.scandir(srcdir('data/'))
                            if 'Ctrl' not in e.name})
treatment_list = [1, 2, 3]


rule all:
    input:
        'perturbation_analysis/all_genes/',
        'perturbation_analysis/per_gene/expression_statistics.csv',
        [f'dce_results/{gene}_{pathway}/{treatment}'
         for gene, pathway in config['pairs']
         for treatment in treatment_list]


rule retrieve_kegg_pathways:
    output:
        graph_files = expand(
            'kegg_pathways/csv_files/{pathway}.csv',
            pathway=set([pathway for _, pathway in config['pairs']]))
    params:
        pathways = set([pathway for _, pathway in config['pairs']])
    script:
        'scripts/retrieve_kegg_pathways.R'


rule analyze_perturbed_genes:
    input:
        graph_files = expand(
            'kegg_pathways/csv_files/{pathway}.csv',
            pathway=set([pathway for _, pathway in config['pairs']]))
    output:
        out_dir = directory('perturbation_analysis/all_genes/')
    params:
        all_pathways = lambda wildcards: [e.path for e in os.scandir('kegg_pathways/csv_files/')],
        perturbed_genes = all_perturbed_genes
    script:
        'scripts/analyze_perturbed_genes.R'


rule analyze_single_perturbed_gene:
    input:
        count_mt_file = srcdir('data/Counts_{gene}_{treatment}.csv')
    output:
        out_dir = directory('perturbation_analysis/per_gene/{gene}/{treatment}/')
    params:
        count_wt_file = lambda wildcards: f'{base_dir}/data/Counts_Ctrl_Epi_{wildcards.treatment}.csv'
    resources:
        mem_mb = 10_000
    script:
        'scripts/analyze_single_perturbed_gene.R'


rule aggregate_expression_statistics:
    input:
        dir_list = expand(
            'perturbation_analysis/per_gene/{gene}/{treatment}/',
            gene=all_perturbed_genes, treatment=treatment_list)
    output:
        fname = 'perturbation_analysis/per_gene/expression_statistics.csv'
    run:
        import pandas as pd

        pd.concat(
            [pd.read_csv(os.path.join(dname, 'expression_stats.csv')) for dname in input.dir_list]
        ).to_csv(output.fname, index=False)


rule compute_dces:
    input:
        count_mt_file = srcdir('data/Counts_{gene}_{treatment}.csv'),
        graph_file = 'kegg_pathways/csv_files/{pathway}.csv'
    output:
        out_dir = directory('dce_results/{gene}_{pathway}/{treatment}')
    params:
        count_wt_file = lambda wildcards: f'{base_dir}/data/Counts_Ctrl_Epi_{wildcards.treatment}.csv'
    resources:
        mem_mb = 10_000
    script:
        'scripts/compute_dces.R'
