import os


configfile: 'config.yaml'
workdir: config['workdir']

all_perturbed_genes = [e.name.split('.')[0].split('_')[-1]
                       for e in os.scandir(srcdir('data/'))
                       if e.name != 'Counts_0_Ctrl.csv']


rule all:
    input:
        'perturbation_analysis/all_genes/',
        'perturbation_analysis/per_gene/expression_statistics.csv',
        [f'dce_results/{gene}_{pathway}/' for gene, pathway in config['pairs']]


rule retrieve_kegg_pathways:
    output:
        graph_files = expand(
            'kegg_pathways/csv_files/{pathway}.csv',
            pathway=set([pathway for _, pathway in config['pairs']]))
    params:
        pathways = set([pathway for _, pathway in config['pairs']])
    script:
        'scripts/retrieve_kegg_pathways.R'


rule analyze_perturbed_genes:
    input:
        graph_files = expand(
            'kegg_pathways/csv_files/{pathway}.csv',
            pathway=set([pathway for _, pathway in config['pairs']]))
    output:
        out_dir = directory('perturbation_analysis/all_genes/')
    params:
        all_pathways = lambda wildcards: [e.path for e in os.scandir('kegg_pathways/csv_files/')],
        perturbed_genes = all_perturbed_genes
    script:
        'scripts/analyze_perturbed_genes.R'


rule analyze_single_perturbed_gene:
    input:
        count_wt_file = srcdir('data/Counts_0_Ctrl.csv'),
        count_mt_file = srcdir('data/Counts_{gene}.csv')
    output:
        out_dir = directory('perturbation_analysis/per_gene/{gene}/')
    script:
        'scripts/analyze_single_perturbed_gene.R'


rule aggregate_expression_statistics:
    input:
        dir_list = expand(
            'perturbation_analysis/per_gene/{gene}/',
            gene=all_perturbed_genes)
    output:
        fname = 'perturbation_analysis/per_gene/expression_statistics.csv'
    run:
        import pandas as pd

        pd.concat(
            [pd.read_csv(os.path.join(dname, 'expression_stats.csv')) for dname in input.dir_list]
        ).to_csv(output.fname, index=False)


rule compute_dces:
    input:
        count_wt_file = srcdir('data/Counts_0_Ctrl.csv'),
        count_mt_file = srcdir('data/Counts_{gene}.csv'),
        graph_file = 'kegg_pathways/csv_files/{pathway}.csv'
    output:
        out_dir = directory('dce_results/{gene}_{pathway}/')
    script:
        'scripts/compute_dces.R'
